class TOSTurboliftHandle : UsableDecoration
{
	Default
	{
		Height 20;
		Radius 8;
		Tag "Turbolift Controls";
	}
	States
	{
		Initial:
			TOS1 A -1 {
				if (special == 0) {
					A_SetSpecial(226, 255, 0, 0, 0, 0);
					user_uses = 1;
				}
			}
			Stop;
	}
}

class TOSTransporterConsole : UsableDecoration
{
	Default
	{
		-SOLID
		Height 36;
		Radius 32;
		Tag "Transporter Console";
	}
	States
	{
		Initial:
			TOS1 A -1 {
				if (special == 0) {
					A_SetSpecial(226, 254, 0, 0, 0, 0);
					user_uses = 1;
				}
				A_SpawnItemEx ("Block8", 0.0, 32.0, 0.0, 0, 0, 0, 0, SXF_TRANSFERSPECIAL);
				A_SpawnItemEx ("Block8", 0.0, -32.0, 0.0, 0, 0, 0, 0, SXF_TRANSFERSPECIAL);
				A_SpawnItemEx ("Block8", 0.0, 32.0, 8.0, 0, 0, 0, 0, SXF_TRANSFERSPECIAL);
				A_SpawnItemEx ("Block8", 0.0, -32.0, 8.0, 0, 0, 0, 0, SXF_TRANSFERSPECIAL);
				A_SpawnItemEx ("Block8", 0.0, 32.0, 16.0, 0, 0, 0, 0, SXF_TRANSFERSPECIAL);
				A_SpawnItemEx ("Block8", 0.0, -32.0, 16.0, 0, 0, 0, 0, SXF_TRANSFERSPECIAL);
				A_SpawnItemEx ("Block8", 0.0, 32.0, 24.0, 0, 0, 0, 0, SXF_TRANSFERSPECIAL);
				A_SpawnItemEx ("Block8", 0.0, -32.0, 24.0, 0, 0, 0, 0, SXF_TRANSFERSPECIAL);
				A_SpawnItemEx ("Block8", 0.0, 32.0, 32.0, 0, 0, 0, 0, SXF_TRANSFERSPECIAL);
				A_SpawnItemEx ("Block8", 0.0, -32.0, 32.0, 0, 0, 0, 0, SXF_TRANSFERSPECIAL);
				A_SpawnItemEx ("Block8", 0.0, 0.0, 32.0, 0, 0, 0, 0, SXF_TRANSFERSPECIAL);
				A_SpawnItemEx ("Block8", 0.0, -16.0, 32.0, 0, 0, 0, 0, SXF_TRANSFERSPECIAL);
				A_SpawnItemEx ("Block8", 0.0, 16.0, 32.0, 0, 0, 0, 0, SXF_TRANSFERSPECIAL);
			}
			Stop;
	}
}


class TOSEngineeringConsole : UsableDecoration
{
	Default
	{
		Radius 16;
		Height 32;
		Tag "Console";
	}
	States
	{
		Initial:
			TOS1 A 0 A_Jump(256, "A", "B", "C");
		A:
			TOS1 A Random(2, 12);
			Goto Initial;
		B:
			TOS1 B Random(2, 12);
			Goto Initial;
		C:
			TOS1 C Random(2, 12);
			Goto Initial;
	}
}

class TOSEngineerConsole : UsableDecoration
{
	Default
	{
		Radius 8;
		Height 32;
		Tag "Console";
	}
	States
	{
		Initial:
			TOS1 A -1 {
				user_width = 64;
				user_uses = args[0] || args[2];
				A_SpawnItemEx ("Block10x32", 0.0, -20.0, 0.0, 0, 0, 0, 0, SXF_TRANSFERSPECIAL);
				A_SpawnItemEx ("Block10x32", 0.0, 20.0, 0.0, 0, 0, 0, 0, SXF_TRANSFERSPECIAL);
			}
			Stop;
	}
}


class TOSCrate : UsableDecoration
{
	string DropClasses[12];

	//$Arg0 "Crate Contents"
	//$Arg0ToolTip "Item to spawn when this crate is opened"
	//$Arg0Type 11
	//$Arg0Enum { 0 = "Nothing"; 1 = "Phaser Charge"; 2 = "Type I Phaser"; 3 = "Type II Phaser"; 4 = "Phaser Charge Pack"; 5 = "Tricorder"; 6 = "Photon Grenade"; 7 = "Photon Grenade Launcher"; 8 = "Access Level 2"; 9 = "Access Level 3"; 10 = "Access Level 4"; 11 = "Medical Kit (Regeneration)"; }
	bool bOpen;

	Default
	{
		-NOGRAVITY
		Radius 12;
		Height 26;
		Health 99;
		Activation THINGSPEC_Switch;
		Tag "Cargo (Crate)";
	}
	States
	{
		Spawn:
			TOS1 A 0 NoDelay {
				DropClasses[0] = "";
				DropClasses[1] = "Charge";
				DropClasses[2] = "PhaserType1"; 
				DropClasses[3] = "PhaserType2"; 
				DropClasses[4] = "ChargePack"; 
				DropClasses[5] = "Tricorder";
				DropClasses[6] = "PhotonGrenadeAmmoBox";
				DropClasses[7] = "PhotonGrenadeLauncher";
				DropClasses[8] = "AccessLevel2"; 
				DropClasses[9] = "AccessLevel3";
				DropClasses[10] = "AccessLevel4";
				DropClasses[11] = "MedKit";

				if (args[1] > 0) {
					return ResolveState("Spawn.Alt");
				}
				return ResolveState(null);
			}
			Goto Super::Spawn;
		Spawn.Alt:
			TOS1 A 0 A_Jump(256, "Spawn.Alt2", "Spawn.Alt3", "Spawn.Alt4");
		Spawn.Alt2:
			TOS2 A 0;
			Goto Super::Initialize;
		Spawn.Alt3:
			TOS3 A 0;
			Goto Super::Initialize;
		Spawn.Alt4:
			TOS4 A 0;
			Goto Super::Initialize;
		Active:
			"####" A 0 {
				bOpen = True;
				if (user_uses > 0) {
					user_uses = 0;
//					ACS_NamedExecuteAlways("A_Drop", 0, args[0]);
					let mo = Spawn(DropClasses[args[0]], Pos, ALLOW_REPLACE);
					mo.A_setSize(mo.Radius * 1.5);
					mo.bDropped = False;
					A_UnSetInvulnerable();
				}
			}
			"####" BCDEF 1;
			Goto InactiveLoop;
		Inactive:
			"####" A 0 {
				if (!bOpen) {
					return ResolveState("InactiveLoop");
				}
				return ResolveState(null);
			}
			"####" EDCBA 1;
			Goto InactiveLoop;
		InactiveLoop:
			"####" # 1;
			Loop;
	}
}

class TOSSecurityConsole : TOSEngineerConsole {}

class TOSComputer : UsableDecoration
{
	Default
	{
		-SOLID
		Height 64;
		Radius 16;
		Tag "Computer Processing Module";
	}
	States
	{
		Initial:
			TOS1 A -1 {
				A_SpawnItemEx ("Block12x54", 12.0, 8.0, 0.0, 0, 0, 0, 0, SXF_TRANSFERSPECIAL);
				A_SpawnItemEx ("Block12x54", 12.0, -8.0, 0.0, 0, 0, 0, 0, SXF_TRANSFERSPECIAL);
			}
			Stop;
	}
}

class TOSBriefComputer : UsableDecoration {}
class TOSComputer2 : UsableDecoration { Default { Height 16; Radius 8; Tag "Computer"; } }
class TOSRockerButtons : UsableDecoration { Default { +SOLID Height 1; Radius 2; } }
class TOSCloset : UsableDecoration
{
	double user_angle;
	double user_destangle;

	Default
	{
		+USESPECIAL
		Radius 16;
		Height 24;
		Activation 1024; //THINGSPEC_Switch;
		Tag "Closet";
	}
	States
	{
		Initial:
			TOS1 A 5 {
				user_uses = 1;
				user_angle = angle;
				user_destangle = angle - 120;
			}
			Loop;
		Active:
		Inactive:
			TOS1 A 1 A_SetAngle(angle - 2);
			TNT1 A 0 { user_angle = user_angle - 2; }
			TOS1 A 0 A_JumpIf(user_angle <= user_destangle, "Spawn");
			Loop;
	}
}
class TOSIntercomAndRockers : UsableDecoration { Default { +SOLID Height 1; Radius 2; } }

class TOSTubeHatch : UsableDecoration
{
	int user_open;

	Default
	{
		+BUMPSPECIAL
		Radius 32;
		Height 2;
		Health 99;
		Activation THINGSPEC_ThingTargets | 1024; //THINGSPEC_Switch;
		Tag "Access Hatch";
	}
	States
	{
		Initial:
			TOS1 A 5;
			Loop;
		Active:
		Inactive:
			TOS1 A 0 {
				if (user_open) {
					return ResolveState("Open");
				} else {
					bBumpSpecial = False;
					return ResolveState(null);
				}
			}
			TOS1 A 30;
			TOS1 B 10 {
					A_PlaySound("door/starfleet", CHAN_AUTO, 1.0);
					A_UnsetSolid();
					user_open = 1;
			}
			TOS1 CDE 10;
			TOS1 F 105;
		Open:
			TOS1 F 35 {
				if (GetDistance(1, AAPTR_TARGET) > 40 * Scale.Y) {
					return ResolveState("Close");
				} else {
					return ResolveState(null);
				}
			}
			Loop;
		Close:
			TOS1 F 0  {
				A_PlaySound("door/starfleet", CHAN_AUTO, 1.0);
				A_SetSolid();
				bBumpSpecial = True;
				user_open = 0;
			}
			TOS1 EDCB 10;
			Goto Initial;
	}
}

class TOSHatch : TOSTubeHatch
{
	Default
	{
		Radius 14;
	}
}

class TOSDilithiumChamber : UsableDecoration
{
	Default
	{
		Radius 6;
		Height 24;
		Health 99;
		Activation THINGSPEC_ThingTargets | 1024; //THINGSPEC_Switch;
		Tag "Dilithium Chamber";
	}
	States
	{
		Initial:
			TOS1 A -1;
			Stop;
		InactiveOpen:
			TOS1 F -1 Light("SmallOrangeLight");
			Stop;
		Active:
			TOS1 A 20;
			TOS1 BCDE 10 Light("SmallOrangeLight");
			TOS1 F 1 Light("SmallOrangeLight");
			Goto InactiveOpen;
		Inactive:
			TOS1 F 20 Light("SmallOrangeLight");
			TOS1 EDCB 10 Light("SmallOrangeLight");
			TOS1 A 1;
			Goto Initial;
	}
}

class TOSSink : UsableDecoration {}

class TOSLadderYellow : UsableDecoration
{
	Default
	{
		-SOLID
		Tag "Ladder";
	}
	States
	{
		Initial:
			TOS1 A 1;
			Goto Super::Initial;
	}

}

class TOSLadderYellowExtension : TOSLadderYellow {}
class TOSLadderRed : TOSLadderYellow {}
class TOSLadderRedExtension : TOSLadderYellow {}

class TOSBridgeConsole1 : UsableDecoration { Default { -SOLID Tag "Console"; Radius 16; Height 96; } }
class TOSBridgeConsole2 : TOSBridgeConsole1 {}
class TOSBridgeConsole3 : TOSBridgeConsole1 {}
class TOSBridgeConsole4 : TOSBridgeConsole1 {}
class TOSBridgeConsole5 : TOSBridgeConsole1 {}
class TOSBridgeConsole6 : TOSBridgeConsole1 {}
class TOSBridgeConsole8 : TOSBridgeConsole1 {}
class TOSBridgeConsoleP1 : TOSBridgeConsole1 {}
class TOSBridgeConsoleP2 : TOSBridgeConsole1 {}
class TOSBridgeConsoleP3 : TOSBridgeConsole1 {}