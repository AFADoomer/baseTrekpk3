/////////////////////////////
// ACTORS FOR REGULAR MAPS // by GuardSoul, optimized and improved by Tormentor667
/////////////////////////////

ACTOR SmokePuff
{
	Height 2
	Radius 1
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+MISSILE
	+NOGRAVITY
	+ROLLSPRITE
	+WINDTHRUST
	RenderStyle "Translucent"
	Alpha 0.0
	States
	{
	Spawn:
		SMOC A 0
		"####" A 3 {
			A_SetRoll(RandomPick(0, 180));
			A_SetScale(ScaleX * FRandom(0.1, 1.0), ScaleY * FRandom(0.1, 1.0));
			ThrustThingZ(0, Random(2,6), 0, 0);
			ThrustThing(Random(0,255), Random(0,1), 0, 0);
		}
	FadeIn:
		"####" AAAAAAA 1 A_FadeIn(0.005)
	FadeLoop:
		"####" A 3 {
			A_SetScale(ScaleX + 0.0125, ScaleY - 0.0125);
			A_FadeOut(.01);
		}
		Loop
	}
}

ACTOR SmokeGenerator : SwitchableDecoration
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	States
	{
	Spawn:
	Active:
		TNT1 A 4 A_SpawnItemEx("SmokePuff", random(-8,8), random(-8,8), 0, 0, 0, 3, 0, SXF_TRANSFERSCALE)
		loop
	Inactive:
		TNT1 A -1
		stop
	EndSpawn:
		TNT1 A 1
		Stop
	}
}

///////////////////
// AMBIENT SMOKE //
///////////////////

ACTOR Ambientsmoke
{
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+NOINTERACTION
	+WINDTHRUST
	RenderStyle "Translucent"
	Alpha 0.001
	Scale 0.3
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2)
		SMOC A 0 A_Jump(256,"End")
		SMOC A 0 A_Jump(256,"End")
	End:
		"####" AAAAAAAAAAAAAAAAAA 1 A_FadeIn(0.005)
		"####" AAAAAAAAAAAAAAAAAAA 3 A_FadeOut(0.005)
		stop
	}
}

ACTOR AmbientSmokeGenerator : SwitchableDecoration
{
	//$Category Special Effects (Wolf3D)
	//$Title Ambient Smoke Generator
	//$Color 12

	//$Arg0 "Radius"
	//$Arg0Tooltip "Radius in map units\nDefault: 0"
	//$Arg1 "Frequency"
	//$Arg1Tooltip "Frequency, the lower the number, the heavier the smoke\nRange: 0 - 255"

	+CLIENTSIDEONLY
	+NOINTERACTION
	States
	{
	Spawn:
//		TNT1 A 0 NODELAY A_JumpIf(CallACS("SmokeSwitch")==0,"EndSpawn")
	Active:
		TNT1 A 2 A_SpawnItemEx("Ambientsmoke", Random(-Args[0], Args[0]), Random(-Args[0], Args[0]), Random(0,48), 0, 0, 0.5, 0, 0, Args[1])
		loop
	Inactive:
		TNT1 A -1
		stop
	EndSpawn:
		TNT1 A 1
		Stop
	}
}