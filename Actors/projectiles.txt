Actor DisruptorBolt
{
	Radius 4
	Height 4
	Speed 40
	Damage 4
	RenderStyle Add
	Alpha 0.85
	Scale 0.75
	PROJECTILE
	DamageType Disruptor
	Decal RailscorchLower1
	SeeSound "weapons/disruptor"
	DeathSound "weapons/plasmax"
	+HITMASTER
	States
	{
		Spawn:
			TOS1 A 0 NoDelay ACS_NamedExecuteAlways("SetRadarTID",0)
		Static:
			TOS1 A 0 A_SetTranslucent(frandom(0.85, 1.0), 1)
			TOS1 A 1 Bright Light("DisruptorLight")
			TOS1 A 0 {
				A_SetPitch(-pitch + 90);
				A_SpawnItemEx("Flare_G", 0, 0, 0, 0, 0, 0, angle, SXF_TRANSFERPITCH);
				A_SetPitch(-pitch + 90);
				if (waterlevel > 0) {
					return State("Death");
				} else {
					return State("");
				}
			}
			Loop
		Death:
			TNT1 A 0 A_SetTranslucent(frandom(0.7, 1.0), 1)
			TNT1 A 0 A_SetScale(FRandom(0.4, 0.6))
			WHIT G 5 Bright Light("DisruptorLight") A_GiveInventory("Disintegration", 1, AAPTR_MASTER)
		FadeLoop:
			WHIT G 1 Bright A_FadeOut(0.2)
			Loop		
	}
}

Actor DisruptorGrenade
{
	Radius 6
	Height 6
	Damage 6
	Projectile
	RenderStyle Add
	Alpha 0.85
	Scale 0.75
	-NOGRAVITY
	Gravity 0.5
	Speed 20
	DamageType Disruptor
	Decal Scorch
	SeeSound "weapons/disruptor/alt"
	DeathSound "weapons/disruptor/explosion"
	States
	{
		Spawn:
			TOS1 A 0 NoDelay ACS_NamedExecuteAlways("SetRadarTID",0)
		Static:
			"####" # 1 Bright Light("DisruptorLight") {
				A_SpawnItemEx("WpnFlare_G", 0, 0, 0, 0, 0, 0, angle);
				if (waterlevel > 0) {
					return State("Death");
				} else {
					return State("");
				}
			}
			Loop
		Death:
			"####" # 1 {
				A_SetScale(Radius / 16.0);
				A_SpawnItemEx("SmokePuff", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERSCALE);
				A_SetScale(1.0);
			}
			"####" B 15 Bright Light("DisruptorLight") {
				A_Explode;
				A_SpawnItemEx("ExplosionSphereWhite", 0, 0, 0, 0, 0, 0, 0);
				A_SpawnItemEx("ExplosionSphereGreen", 0, 0, 0, 0, 0, 0, 0);
				A_Quake(5, 35, 128, 256);
				A_PlaySound("effects/explosion/distance", CHAN_AUTO, 0.5, 0, ATTN_NONE);
			}
			Stop
	}
}

Actor PhotonGrenade
{
	Radius 6
	Height 6
	Damage 25
	Projectile
	RenderStyle Add
	Alpha 0.85
	Scale 0.75
	-NOGRAVITY
	+SEEKERMISSILE
	Gravity 0.05
	Speed 18
	ActiveSound "weapons/photon_active"
	DeathSound "weapons/photon_hit"
	Decal Scorch
	States
	{
		Spawn:
			TOS1 A 0 NoDelay ACS_NamedExecuteAlways("SetRadarTID",0)
			TOS1 A 5 Bright A_LoopActiveSound
			TOS1 A 0 A_Jump(256, "Static")
		Static:
			TOS1 A 1 Bright {
				A_SpawnItemEx("Flare_B", 0, 0, 0, 0, 0, 0, angle);
				A_SpawnItemEx("PhotonGrenadeShadow", 0, 0, 0, 0, 0, 0, 0);
				if (waterlevel > 0) {
					return State("Death");
				} else {
					return State("");
				}
			}
			Loop
		Death:
			"####" # 1 {
				A_SetScale(Radius / 16.0);
				A_SpawnItemEx("SmokePuff", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERSCALE);
				A_SetScale(1.0);
			}
			TOS1 B 15 Bright {
				A_Explode;
				A_SpawnItemEx("ExplosionSphereYellow", 0, 0, 0, 0, 0, 0, 0);
				A_SpawnItemEx("ExplosionSphereBlue", 0, 0, 0, 0, 0, 0, 0);
				A_Quake(9, 35, 128, 256);
				A_PlaySound("effects/explosion/distance", CHAN_AUTO, 1.0, 0, ATTN_NONE);
			}
			Stop
	}
}

Actor PhotonGrenadeHoming : PhotonGrenade
{
	var int user_timer;

	States
	{
		Static:
			TOS1 A 1 Bright {
				A_SeekerMissile(0, 4, SMF_LOOK | SMF_PRECISE, 256, 10);
				A_SpawnItemEx("Flare_B", 0, 0, 0, 0, 0, 0, angle);
				A_SpawnItemEx("PhotonGrenadeShadow2", 0, 0, 0, 0, 0, 0, 0);
				if (waterlevel > 0 || user_timer > 115) {
					return State("Death");
				} else {
					user_timer++;
					return State("");
				}
			}
			Loop
	}
}

Actor LaserBlast
{
	Radius 6
	Height 8
	FastSpeed 20
	Projectile
	RenderStyle Add
	Alpha 1
	Damage 1
	Speed 35
	Translation "215:215=[235, 100, 215]:[235, 100, 215]", "249:249=[235, 100, 215]:[235, 100, 215]"
	States
	{
		Spawn:
			TOS1 A 0 NoDelay ACS_NamedExecuteAlways("SetRadarTID",0)
		Static:
			BLAS A 1 BRIGHT A_SpawnItemEx("LaserTrail", 0, 0, 0)
			Loop
	}
}

Actor Trail
{
	var int user_spawn;
	
	Radius 32
	Height 6
	Damage 40
	Projectile
	Speed 7
	Decal Scorch
	ActiveSound ""
	+SEEKERMISSILE
	RenderStyle Add
	Alpha 0.8
	DamageType Electric
	States
	{
		Spawn:
			TOS1 A 1 Bright
			"####" # 0 A_PlaySound("effectss/electric", CHAN_7, 1.0, 1)
			"####" # 0 A_SpawnItemEx("TrailShell", 0, 0, 0)
			"####" # 0 A_SetUserVar("user_spawn", user_spawn + 1)
			"####" # 0 A_JumpIf(user_spawn > 15, "Static")
			Goto Spawn
		Static:
			TOS1 A 1 Bright
			"####" # 0 A_SeekerMissile(0, 1, SMF_LOOK | SMF_PRECISE, 128, 1)
			"####" # 0 A_SpawnItemEx("TrailShell", 0, 0, 0)
			"####" # 0 A_CustomRailgun(1, 0, "None", "White", RGF_CENTERZ | RGF_NOPIERCING | RGF_FULLBRIGHT | RGF_EXPLICITANGLE | RGF_SILENT, 0, 100.0, "ElectricalHit", Random(0.0, 360.0), Random(225.0, 315.0), 100, 1, 0.1, 0)
			"####" # 0 A_CustomRailgun(1, 0, "None", "White", RGF_CENTERZ | RGF_NOPIERCING | RGF_FULLBRIGHT | RGF_EXPLICITANGLE | RGF_SILENT, 0, 100.0, "ElectricalHit", Random(0.0, 360.0), Random(225.0, 315.0), 100, 1, 0.1, 0)
			"####" # 0 A_CustomRailgun(0, 0, "None", 994499, RGF_CENTERZ | RGF_NOPIERCING | RGF_FULLBRIGHT | RGF_EXPLICITANGLE | RGF_SILENT, 0, 100.0, "ElectricalHit", Random(0.0, 360.0), Random(0.0, 360.0), 15, 1, 0.1, 0)
			Loop
		Death:
			"####" # 0 A_CustomRailgun(10, 0, "None", "White", RGF_CENTERZ | RGF_NOPIERCING | RGF_FULLBRIGHT | RGF_EXPLICITANGLE | RGF_SILENT, 1, 100.0, "ElectricalHit", 90.0, Random(0.0, 360.0), 100, 5, 0.1, 0)
			"####" # 0 A_CustomRailgun(10, 0, "None", "White", RGF_CENTERZ | RGF_NOPIERCING | RGF_FULLBRIGHT | RGF_EXPLICITANGLE | RGF_SILENT, 1, 100.0, "ElectricalHit", 90.0, Random(0.0, 360.0), 100, 5, 0.1, 0)
			"####" # 0 A_CustomRailgun(10, 0, "None", "White", RGF_CENTERZ | RGF_NOPIERCING | RGF_FULLBRIGHT | RGF_EXPLICITANGLE | RGF_SILENT, 1, 100.0, "ElectricalHit", 90.0, Random(0.0, 360.0), 100, 5, 0.1, 0)
			"####" # 0 A_CustomRailgun(5, 0, "None", "White", RGF_CENTERZ | RGF_NOPIERCING | RGF_FULLBRIGHT | RGF_EXPLICITANGLE | RGF_SILENT, 1, 100.0, "ElectricalHit", Random(90.0, 270.0), Random(0.0, 360.0), 400, 5, 0.2, 0)
			"####" # 0 A_CustomRailgun(5, 0, "None", "White", RGF_CENTERZ | RGF_NOPIERCING | RGF_FULLBRIGHT | RGF_EXPLICITANGLE | RGF_SILENT, 1, 100.0, "ElectricalHit", Random(90.0, 270.0), Random(0.0, 360.0), 400, 5, 0.2, 0)
			"####" # 0 A_CustomRailgun(5, 0, "None", "White", RGF_CENTERZ | RGF_NOPIERCING | RGF_FULLBRIGHT | RGF_EXPLICITANGLE | RGF_SILENT, 1, 100.0, "ElectricalHit", Random(90.0, 270.0), Random(0.0, 360.0), 400, 5, 0.2, 0)
			"####" # 0 A_SpawnItemEx("ExplosionSphereWhite2", 0, 0, 0, 0, 0, 0, 0)
			"####" # 0 A_StopSound(CHAN_7)
			"####" # 0 A_Quake(8, 70, 512, 512, "effects/explosion")
			TNT1 A 80 A_Explode(2048, 512, 0, 1, 256)
			Stop
	}
}