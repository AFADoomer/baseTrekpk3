ACTOR Charge : Ammo replaces Clip 26001
{
	Radius 10
	Height 20
	+SHOOTABLE
	+NODAMAGE
	+NOBLOOD
	Inventory.Amount 10
	Inventory.MaxAmount 100
	Ammo.BackpackAmount 20
	Ammo.BackpackMaxAmount 200
	Inventory.Icon "CHARA0"
	Tag "Phaser Ammunition"
	Inventory.PickupMessage "$Charge"
	States
	{
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteAlways("SetRadarTID",0)
			CHAR A -1
			Stop
	}
}

ACTOR ChargePack : Charge replaces ClipBox 26007
{
	Inventory.Amount 30
	Tag "Phaser Ammunition Pack"
	Inventory.PickupMessage "$Charges"
	States
	{
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteAlways("SetRadarTID",0)
			CHAR A -1
			Stop
	}
}

ACTOR RifleCharge : Ammo replaces Cell 26006
{
	Radius 10
	Height 20
	+SHOOTABLE
	+NODAMAGE
	+NOBLOOD
	Inventory.Amount 10
	Inventory.MaxAmount 100
	Ammo.BackpackAmount 20
	Ammo.BackpackMaxAmount 200
	Inventory.Icon "CHARP0"
	Tag "Phaser Rifle Ammunition"
	Inventory.PickupMessage "$RifleCharge"
	States
	{
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteAlways("SetRadarTID",0)
			CHAR P -1
			Stop
	}
}

ACTOR RifleChargePack : Ammo replaces CellPack
{
	Radius 10
	Height 20
	Inventory.Amount 20
	Inventory.PickupMessage "$RifleCharges"
}

ACTOR PhotonGrenadeAmmo : Ammo replaces RocketAmmo 26002
{
	Radius 10
	Height 20
	+SHOOTABLE
	+NODAMAGE
	+NOBLOOD
	Inventory.Amount 1
	Inventory.MaxAmount 12
	Ammo.BackpackAmount 24
	Ammo.BackpackMaxAmount 48
	Inventory.Icon "CHARB0"
	Tag "Photon Grenades"
	Inventory.PickupMessage "$PhotonGrenade"
	States
	{
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteAlways("SetRadarTID",0)
			CHAR B -1
			Stop
	}
}

ACTOR PhotonGrenadeAmmoBox : PhotonGrenadeAmmo replaces RocketBox 26003
{
	Radius 10
	Height 20
	Inventory.Amount 6
	Inventory.PickupMessage "$PhotonGrenades"
	Tag "Photon Grenades"
}

ACTOR DisruptorCharge : Ammo replaces Shell 26004
{
	Radius 10
	Height 20
	+SHOOTABLE
	+NODAMAGE
	+NOBLOOD
	Inventory.Amount 10
	Inventory.MaxAmount 100
	Ammo.BackpackAmount 20
	Ammo.BackpackMaxAmount 200
	Inventory.Icon "CHARG0"
	Tag "Disruptor Ammunition"
	Inventory.PickupMessage "$DisruptorCharge"
	States
	{
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteAlways("SetRadarTID",0)
			CHAR G -1
			Stop
	}
}

ACTOR DisruptorChargeBox : Ammo replaces ShellBox 26005
{
	Inventory.Amount 25
	Inventory.PickupMessage "$DisruptorCharges"
	Tag "Disruptor Ammunition"
}

Actor PhaserType1 : Weapon 25000
{
	+SHOOTABLE
	+NODAMAGE
	+NOBLOOD
	Radius 10
	Height 20
	Obituary "%o got phasered by %k."
	Weapon.SelectionOrder 200
	Weapon.SlotNumber 2
	Weapon.AmmoType "Charge"
	Weapon.AmmoType2 "Charge"
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 2
	Weapon.AmmoGive 100
	Weapon.BobStyle Alpha
	+Weapon.Ammo_Optional
	+Weapon.Alt_Ammo_Optional
	Tag "$Phaser1"
	Decal "RailScorchLower1"
	Inventory.PickupMessage "$Phaser1"
	States
	{
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteAlways("SetRadarTID",0)
			PHAS P -1
			Stop
		Ready:
			PHAS A 1 A_WeaponReady
			Loop
		Deselect:
			"####" # 0 A_StopSound(CHAN_WEAPON)
			PHAS A 1 A_Lower
			Loop
		Select:
			PHAS A 1 A_Raise
			Loop
		Fire:
			PHAS BCD 1
			PHAS A 0 A_PlaySound("weapons/phaser", CHAN_WEAPON, 1.0, True)
		Hold:
			PHAS A 0 A_JumpIfInventory ("Charge", 2, 1)
			Goto Sputter
			PHAS E 1 Bright
			PHAS A 0 A_RailAttack(0, 0, 0, "None", "AA AA DD", RGF_CENTERZ | RGF_NOPIERCING | RGF_FULLBRIGHT | RGF_SILENT, 0, "None", 0, 0, 2048, 1, 1.0, 0, "None", 16)
			PHAS A 0 A_RailAttack(3, 0, 1, "None", "None", RGF_CENTERZ | RGF_NOPIERCING | RGF_FULLBRIGHT | RGF_SILENT, 0, "PhaserHit", 0, 0, 2048, 0, 3.0, 0, "BluePhaserParticle", 16)
			TNT1 A 0 A_ReFire
			"####" # 0 A_StopSound(CHAN_WEAPON)
			PHAS E 8 Bright
			PHAS DCB 1
			Goto Ready
		Sputter:
			PHAS A 0 A_PlaySound("weapons/phaser_empty", CHAN_WEAPON, 1.0, True)
			PHAS E 1 Bright
			PHAS A 0 A_RailAttack(0, 0, 0, "None", "None", RGF_CENTERZ | RGF_NOPIERCING | RGF_FULLBRIGHT | RGF_SILENT, 0, "None", 0, 0, 512, 0, frandom(2.0, 3.0), 0, "BluePhaserParticle", 16)
			PHAS A 0 A_RailAttack(1, 0, 0, "None", "None", RGF_CENTERZ | RGF_NOPIERCING | RGF_FULLBRIGHT | RGF_SILENT, 0, "PhaserHit", 0, 0, 512, 0, frandom(3.0, 4.0), 0, "WhitePhaserParticle", 16)
			TNT1 A 0 A_ReFire ("Hold")
			"####" # 0 A_StopSound(CHAN_WEAPON)
			PHAS E 8 Bright
			PHAS DCB 1
			Goto Ready
		AltFire:
			PHAS A 0 A_PlaySound("weapons/phaser", CHAN_WEAPON, 1.0, True)
		AltHold:
			PHAS A 0 A_JumpIfInventory ("Charge", 6, 1)
			Goto AltSputter
			PHAS E 1 Bright
			PHAS A 0 A_RailAttack(0, 0, 0, "None", "FF AA AA", RGF_CENTERZ | RGF_NOPIERCING | RGF_FULLBRIGHT | RGF_SILENT, 0, "None", 0, 0, 2048, 1, 1.0, 0, "None", 16)
			PHAS A 0 A_RailAttack(0, 0, 1, "None", "None", RGF_CENTERZ | RGF_NOPIERCING | RGF_FULLBRIGHT | RGF_SILENT, 0, "PhaserStunHit", 0, 0, 0, 2048, 2.5, 0, "OrangePhaserParticle", 16)
			TNT1 A 0 A_ReFire
			"####" # 0 A_StopSound(CHAN_WEAPON)
			PHAS E 8 Bright
			PHAS DCB 1
			Goto Ready
		AltSputter:
			PHAS A 0 A_PlaySound("weapons/phaser_empty", CHAN_WEAPON, 1.0, True)
			PHAS E 1 Bright
			PHAS A 0 A_RailAttack(1, 0, 0, "None", "None", RGF_CENTERZ | RGF_NOPIERCING | RGF_FULLBRIGHT | RGF_SILENT, 0, "None", 0, 0, 512, 0, frandom(2.0, 3.0), 0, "RedPhaserParticle", 16)
			PHAS A 0 A_RailAttack(0, 0, 0, "None", "None", RGF_CENTERZ | RGF_NOPIERCING | RGF_FULLBRIGHT | RGF_SILENT, 0, "PhaserStunHit", 0, 0, 512, 0, frandom(3.0, 4.0), 0, "OrangePhaserParticle", 16)
			TNT1 A 0 A_ReFire("AltHold")
			"####" # 0 A_StopSound(CHAN_WEAPON)
			PHAS E 8 Bright
			PHAS DCB 1
			Goto Ready
	}
}

Actor PhaserType2 : Weapon 25001
{
	+SHOOTABLE
	+NODAMAGE
	+NOBLOOD
	Radius 10
	Height 20
	Obituary "%o got phasered by %k."
	Weapon.SelectionOrder 200
	Weapon.SlotNumber 2
	Weapon.AmmoType "Charge"
	Weapon.AmmoType2 "Charge"
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 2
	Weapon.AmmoGive 100
	Weapon.BobStyle Alpha
	Weapon.SisterWeapon "PhaserType1"
	Tag "$Phaser2"
	Decal "RailScorchLower1"
	Inventory.PickupMessage "$Phaser2"
	States
	{
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteAlways("SetRadarTID",0)
			PHAS P -1
			Stop
		Ready:
			PHAS A 1 A_WeaponReady
			Loop
		Deselect:
			"####" # 0 A_StopSound(CHAN_WEAPON)
			PHAS A 1 A_Lower
			Loop
		Select:
			PHAS A 1 A_Raise
			Loop
		Fire:
			PHAS BCD 1
			PHAS A 0 A_PlaySound("weapons/phaser", CHAN_WEAPON, 1.0, True)
		Hold:
			PHAS E 1 Bright
			PHAS A 0 A_RailAttack(0, 0, 0, "None", "AA AA DD", RGF_CENTERZ | RGF_NOPIERCING | RGF_FULLBRIGHT | RGF_SILENT, 0, "None", 0, 0, 2048, 1, 1.0, 0, "None", 16)
			PHAS A 0 A_RailAttack(4, 0, 1, "None", "None", RGF_CENTERZ | RGF_NOPIERCING | RGF_FULLBRIGHT | RGF_SILENT, 0, "PhaserHit", 0, 0, 2048, 0, 3.0, 0, "BluePhaserParticle", 16)
			TNT1 A 0 A_ReFire
			"####" # 0 A_StopSound(CHAN_WEAPON)
			PHAS E 8 Bright
			PHAS DCB 1
			Goto Ready
		AltFire:
			PHAS BCD 1
			PHAS A 0 A_PlaySound("weapons/phaser_alt", CHAN_WEAPON, 1.0, True)
		AltHold:
			PHAS E 1 Bright
			PHAS A 0 A_RailAttack(0, 0, 0, "None", "FF AA AA", RGF_CENTERZ | RGF_NOPIERCING | RGF_FULLBRIGHT | RGF_SILENT, 0, "None", 0, 0, 2048, 1, 1.0, 0, "None", 16)
			PHAS A 0 A_RailAttack(10, 0, 1, "None", "None", RGF_CENTERZ | RGF_NOPIERCING | RGF_FULLBRIGHT | RGF_SILENT, 0, "PhaserDisintegrateHit", 0, 0, 2048, 0, 2.5, 0, "RedPhaserParticle", 16)
			TNT1 A 0 A_ReFire
			"####" # 0 A_StopSound(CHAN_WEAPON)
			PHAS E 8 Bright
			PHAS DCB 1
			Goto Ready
	}
}

Actor Disruptor : Weapon Replaces Shotgun 25002
{
	+SHOOTABLE
	+NODAMAGE
	+NOBLOOD
	Radius 10
	Height 20
	Obituary "%o got disrupted by %k."
	Weapon.SelectionOrder 300
	Weapon.SlotNumber 4
	Weapon.AmmoType "DisruptorCharge"
	Weapon.AmmoType2 "DisruptorCharge"
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 10
	Weapon.AmmoGive 100
	Weapon.BobStyle Alpha
	Tag "$Disruptor"
	Inventory.PickupMessage "$Disruptor"
	States
	{
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteAlways("SetRadarTID",0)
			PHAS P -1
			Stop
		Ready:
			PHAS "#" 1 A_WeaponReady
			Loop
		Deselect:
			PHAS A 1 A_Lower
			Loop
		Select:
			PHAS A 1 A_Raise
			Loop
		Fire:
			PHAS BCD 1
			"####" # 0 A_SetArg(1, 1)
			PHAS E 1 Bright A_FireCustomMissile("DisruptorBolt", 0, 1, 0, 16, FPF_AIMATANGLE/* | FPF_TRANSFERPITCH*/, 0)
			PHAS D 6
			TNT1 A 0 A_ReFire
			PHAS CB 1
			Goto Ready
		Hold:
			"####" # 0 A_SetArg(1, Args[1] + 1)
			"####" # 0 A_Jump(64, "Hold.Wild")
			PHAS E 1 Bright A_FireCustomMissile("DisruptorBolt", FRandom(-1.0, 1.0), 1, 0, 16, FPF_AIMATANGLE/* | FPF_TRANSFERPITCH*/, FRandom(-1.0, 1.0))
			Goto Hold.Resume
		Hold.Wild:
			PHAS E 1 Bright A_FireCustomMissile("DisruptorBolt", FRandom(-3.0, 3.0), 1, 0, 16, FPF_AIMATANGLE, FRandom(-2.0, 2.0))
		Hold.Resume:
			"####" # 0 A_JumpIf(Args[1] >= 3, "Resume")
			PHAS D 6
			TNT1 A 0 A_ReFire
			PHAS CB 1
			Goto Ready
		Resume:
			PHAS D 16
			"####" # 0 A_SetArg(1, 0)
			Goto Ready
		AltFire:
			PHAS BCD 1
			"####" # 0 A_SetArg(0, 0)
		AltHold:
			"####" # 0 A_SetArg(0, Args[0] + 5)
			PHAS D 5
			"####" # 0 A_JumpIf(Args[0] > 30, "AltFireActual")
			TNT1 A 0 A_ReFire
		AltFireActual:
			PHAS E 1 Bright A_FireCustomMissile("DisruptorGrenade", Random(-1.0, 1.0), 1, 0, 16, 1, Args[0])
			PHAS D 16
			"####" # 0 A_SetArg(0, 0)
			Goto Resume
	}
}

Actor PhotonGrenadeLauncher : Weapon Replaces RocketLauncher 25003 
{
	+SHOOTABLE
	+NODAMAGE
	+NOBLOOD
	Radius 10
	Height 20
	Obituary "%o got grenaded by %k."
	Weapon.SelectionOrder 500
	Weapon.SlotNumber 5
	Weapon.AmmoType "PhotonGrenadeAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.AmmoType2 "PhotonGrenadeAmmo"
	Weapon.AmmoUse2 1
	Weapon.BobStyle Alpha
	Tag "$PhotonGrenadeLauncher"
	Inventory.PickupMessage "$PhotonGrenadeLauncher"
	States
	{
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteAlways("SetRadarTID",0)
			PHAS P -1
			Stop
		Ready:
			PHAS A 1 A_WeaponReady
			Loop
		Deselect:
			PHAS A 1 A_Lower
			Loop
		Select:
			PHAS A 1 A_Raise
			Loop
		Fire:
			PHAS BCD 1
		Hold:
			PHAS D 24
			PHAS E 1 Bright A_FireCustomMissile("PhotonGrenade", Random(-1.0, 1.0), 1, 0, 16, 1, FRandom(-1.0, 1.0))
			PHAS E 0 A_PlaySound("weapons/photon", CHAN_WEAPON)
			PHAS D 0 A_Refire
			PHAS DCB 1
			Goto Ready
		AltFire:
			PHAS BCD 1
		AltHold:
			PHAS D 24
			PHAS E 24 A_PlaySound("computer/button2", CHAN_WEAPON)
			PHAS E 1 Bright A_FireCustomMissile("PhotonGrenadeHoming", Random(-1.0, 1.0), 1, 0, 16, 1, Random(-1.0, 1.0))
			PHAS E 0 A_PlaySound("weapons/photon", CHAN_WEAPON)
			PHAS D 0 A_Refire
			PHAS DCB 1
			Goto Ready
	}
}

Actor PhaserRifle : Weapon Replaces Chaingun 25004
{
	+SHOOTABLE
	+NODAMAGE
	+NOBLOOD
	Radius 10
	Height 20
	Obituary "%o got phasered by %k."
	Weapon.SelectionOrder 150
	Weapon.SlotNumber 3
	Weapon.AmmoType "RifleCharge"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 100
	Weapon.BobStyle Alpha
	Tag "$PhaserRifle"
	Decal "RailScorchLower1"
	Inventory.PickupMessage "$PhaserRifle"
	States
	{
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteAlways("SetRadarTID",0)
			PHAS P -1
			Stop
		Ready:
			PHAS A 1 A_WeaponReady
			Loop
		Deselect:
			"####" # 0 A_StopSound(CHAN_WEAPON)
			PHAS A 1 A_Lower
			Loop
		Select:
			PHAS A 1 A_Raise
			Loop
		Fire:
			PHAS BCD 1
			PHAS A 0 A_PlaySound("weapons/rifle", CHAN_WEAPON, 1.0, True)
		Hold:
			PHAS E 2 Bright
			PHAS A 0 A_RailAttack(0, 0, 0, "None", "EB 64 D7", RGF_CENTERZ | RGF_NOPIERCING | RGF_FULLBRIGHT | RGF_SILENT, 0, "None", 0, 0, 2048, 1, 1.0, 0, "None", 16)
			PHAS A 0 A_RailAttack(20, 0, 1, "None", "None", RGF_CENTERZ | RGF_NOPIERCING | RGF_FULLBRIGHT | RGF_SILENT, 0, "PhaserHitW", 0, 0, 2048, 1, 3.0, 0, "LaserTrail", 16)
			TNT1 A 0 A_ReFire
			"####" # 0 A_StopSound(CHAN_WEAPON)
			PHAS E 8 Bright
			PHAS DCB 1
			Goto Ready
	}
}

Actor Tricorder : Weapon Replaces AllMap 25010
{
	+SHOOTABLE
	+NODAMAGE
	+NOBLOOD
	Radius 10
	Height 20
	Obituary "How did %o get killed by %k's tricorder?"
	Weapon.SelectionOrder 10000
	Weapon.SlotNumber 0
	Weapon.BobStyle Alpha
	Tag "$Tricorder"
	Inventory.PickupMessage "$Tricorder"
	States
	{
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteAlways("SetRadarTID", 0)
			PHAS P -1
			Stop
		Ready:
			TRIC W 1 A_WeaponReady(WRF_NOBOB)
			Loop
		Deselect:
			"####" # 1 A_Lower
			Loop
		Select:
			TRIC W 1 A_Raise
			Loop
		Fire:
			"####" # 0 A_PlaySound("actor/tricorder", CHAN_WEAPON, 0.7, True)
		Hold:
			"####" # 1 ACS_NamedExecuteAlways("PrintInfo", 0)
			"####" # 0 A_Refire
			"####" # 0 A_StopSound(CHAN_WEAPON)
			Goto Ready
		AltFire:
			"####" # 1 A_KillChildren
			"####" # 0 A_SpawnItemEx("NavGoal", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER)
			Goto Ready
	}
}

Actor Punch : Weapon
{
	Radius 10
	Height 20
	Obituary "%o was pummelled to death by %k's fists."
	Weapon.SelectionOrder 9000
	Weapon.SlotNumber 1
	Weapon.BobStyle Alpha
	States
	{
		Spawn:
			TNT1 A -1
			Stop
		Ready:
			TOS1 A 1 A_WeaponReady
			Loop
		Deselect:
			TOS1 A 1 A_Lower
			Loop
		Select:
			TOS1 A 1 A_Raise
			Loop
		AltFire:
			TOS1 A 0 A_PlaySound("actor/punch", CHAN_WEAPON, 1.0)
			TOS1 BCDEF 4
			TOS1 G 8 A_CustomPunch(4 * random(1, 10), TRUE)
			TOS1 HIJK 3
			Goto Ready
		Fire:
			TOS1 LM 3
			TOS1 N 8 A_CustomPunch(2 * random(1, 10), TRUE)
			TOS1 OPQ 3
			TOS1 A 0 A_Refire
			TOS1 A 0 A_StopSound(CHAN_WEAPON)
			Goto Ready
	}
}

ACTOR PhotonTorpedoShot : Ammo
{
	+SHOOTABLE
	+NODAMAGE
	+NOBLOOD
	Inventory.Amount 1
	Inventory.MaxAmount 2
	Ammo.BackpackAmount 2
	Ammo.BackpackMaxAmount 4
	Inventory.Icon "TNT1A0"
}

Actor Communicator : Weapon Replaces BFG9000
{
	+SHOOTABLE
	+NODAMAGE

	Radius 10
	Height 20
	Obituary "%o got torpedoed by %k."
	Weapon.SelectionOrder 500
	Weapon.SlotNumber 7
	Weapon.AmmoType "PhotonTorpedoShot"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.BobStyle Alpha
	Tag "$PhotonTorpedo"
	Inventory.PickupMessage "$PhotonTorpedo"
	States
	{
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteAlways("SetRadarTID",0)
			PHAS P -1
			Stop
		Ready:
			PHAS GGGHHHIIIJJJKKKLLLMMMNNNOOO 2 A_WeaponReady
			Loop
		Deselect:
			PHAS FEDCBAAAAAAAAAAAAAA 1 A_Lower
			Goto Ready
		Select:
			PHAS AAAAAAAAAAAAAABBCCDDEEFFF 1 A_Raise
			Goto Ready
		Fire:
			PHAS F 1 A_JumpIf(CallACS("A_CheckOutdoors") == 0, "Ready")
			PHAS F 1 A_FireCustomMissile("Trail", Random(-1, 1), 1, 0, ceilingz - z - height - 32, 1, 0)
			PHAS F 24
			Goto Ready
	}
}