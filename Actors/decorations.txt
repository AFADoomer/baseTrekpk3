Actor TOSDecoration
{
	var int user_uses;
	var int user_width;
	var int user_height;
	var int user_disintegrationcolor;
	var float user_scaleX;
	var float user_scaleY;
	var int user_destroyed;
	var int user_fadeincount;

	+NOGRAVITY
	-SOLID
	+SHOOTABLE
	+NOBLOOD
	+INVULNERABLE
	+NODAMAGE
	+DONTTHRUST
	+NOTAUTOAIMED
	Height 16
	Radius 8
	Health 99999
	Mass 999999
	Species "Decoration"
	DeathSound "effects/explosion/debris"
	States
	{
		Spawn:
			TOS1 A 1
			"####" # 0 {
				ACS_NamedExecuteAlways("SetRadarTID",0);
				ACS_NamedExecuteAlways("A_SetupThing",0);
				A_SetUserVar("user_uses", args[0] || args[2]);
				user_scaleX = scaleX;
				user_scaleY = scaleY;
				if (GetCVar("g_bounding") && A_CheckFlag("SOLID", "Null")) {
					A_SetScale(Radius * 2, Height * 1.2, AAPTR_DEFAULT, TRUE);
					A_SpawnItemEx ("Block1x1Bounding", 0, 0, Height / 2, 0, 0, 0, 0, SXF_TRANSFERSCALE | SXF_SETMASTER);
					A_SetScale(user_scaleX, user_scaleY);
				}
				return State("Static");
			}
		Pain.Stun:
		Static:
			"####" # 1 {
				if (velx != 0 || vely != 0 || velz != 0) {
					return State("Moved");
				} else {
					return State("");
				}
			}
			Loop
		Pain:
			"####" # 1 {
				A_Quake(Radius / 16, Radius / 3, Radius, Radius * 3);
				A_Explode(Radius / 16, Radius / 2, 0, 1);
				A_SetScale(Radius / 32.0);
				if (!user_destroyed && (GetSpawnHealth() >= 300 / user_scalex)) {
					A_SpawnItemEx("FireSpawner", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERSCALE);
				} else if (GetSpawnHealth() >= 100 / user_scalex) {
					A_SpawnItemEx("FlareUp", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERSCALE);
				}
				A_SpawnItemEx("SmokePuff", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERSCALE);
				A_SetScale(1.0);
				user_destroyed == 1;
			}
			Goto Static
		Moved:
			"####" # 5 A_Gravity
			Goto Static
		Death:
			"####" # 0 {
				user_disintegrationcolor = 16711590;
				A_KillChildren;
			}
			"####" # 0 A_Jump(256, "DeathResume")
		Death.Phaser:
			"####" # 0 {
				user_disintegrationcolor = 10803452;
				A_KillChildren("Phaser");
			}
			"####" # 0 A_Jump(256, "DeathResume")
		Death.PhaserRifle:
			"####" # 0 {
				user_disintegrationcolor = 16557295;
				A_KillChildren("PhaserRifle");
			}
			"####" # 0 A_Jump(256, "DeathResume")
		Death.Disruptor:
			"####" #  0 {
				user_disintegrationcolor = 2620994;
				A_KillChildren("Disruptor");
			}
			"####" # 0 A_Jump(256, "DeathResume")
		DeathResume:
			"####" # 45 {
				if (Radius >= 16) {
					A_Scream;
				}
				A_RemoveChildren(TRUE, RMVF_MISC, "None", "Block");
				ACS_NamedExecuteAlways("A_Disintegrate", 0, user_disintegrationcolor, 1);
				A_ChangeFlag("NOGRAVITY", FALSE);
				A_SetPitch(FRandom(-30.0, 30.0), SPF_INTERPOLATE);
				A_SetRoll(FRandom(-30.0, 30.0), SPF_INTERPOLATE);
			}
			Stop
	}
}

Actor TOSDestroyableDecoration : TOSDecoration
{
	//$Sprite TOS1A0
	Health 10
	Mass 50 
	-INVULNERABLE
	-NODAMAGE
	-DONTTHRUST 
}
Actor TOSSwitchableDecoration : SwitchableDecoration
{
	var int user_uses;
	var int user_width;
	var int user_height;
	var int user_disintegrationcolor;
	var float user_scaleX;
	var float user_scaleY;

	+NOGRAVITY
	+SOLID
	+SHOOTABLE
	+INVULNERABLE
	+NOBLOOD
	+NODAMAGE
	+DONTTHRUST

	Height 0
	Radius 0
	Mass 999999

	States
	{
		Spawn:
			TOS1 A 1
			"####" # 0 {
				ACS_NamedExecuteAlways("SetRadarTID",0);
				ACS_NamedExecuteAlways("A_SetupThing",0);
				A_SetUserVar("user_uses", args[0] || args[2]);
				user_scaleX = scaleX;
				user_scaleY = scaleY;
				if (GetCVar("g_bounding") && A_CheckFlag("SOLID", "Null")) {
					A_SetScale(Radius * 2, Height * 1.2, AAPTR_DEFAULT, TRUE);
					A_SpawnItemEx ("Block1x1Bounding", 0, 0, Height / 2, 0, 0, 0, 0, SXF_TRANSFERSCALE | SXF_SETMASTER);
					A_SetScale(user_scaleX, user_scaleY);
				}
				if (special > 0) { A_ChangeFlag("NOTAUTOAIMED", FALSE); }
				return State("Static");
			}
		Static:
			"####" # 1 //A_JumpIf((velx != 0 || vely != 0 || velz != 0) && special == 0 && health > 0, "Moved")
			Loop
		Pain:
			"####" # 1 {
				A_Quake(Radius / 16, Radius / 3, Radius, Radius * 3);
				A_Explode(Radius / 16, Radius / 2, 0, 1);
				A_SetScale(Radius / 32.0);
				if (GetSpawnHealth() >= 300) {
					A_SpawnItemEx("FireSpawner", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERSCALE);
				} else if (GetSpawnHealth() >= 100) {
					A_SpawnItemEx("FlareUp", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERSCALE);
				}
				A_SpawnItemEx("SmokePuff", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERSCALE);
				A_SetScale(1.0);
			}
			Goto Static
		Moved:
			"####" # 5 A_Gravity
			Goto Static
		Death:
			"####" # 0 {
				A_SetUserVar("user_disintegrationcolor", 16711590);
				A_KillChildren;
			}
			"####" # 0 A_Jump(256, "DeathResume")
		Death.Phaser:
			"####" # 0 {
				A_SetUserVar("user_disintegrationcolor", 10803452);
				A_KillChildren("Phaser");
			}
			"####" # 0 A_Jump(256, "DeathResume")
		Death.PhaserRifle:
			"####" # 0 {
				A_SetUserVar("user_disintegrationcolor", 16557295);
				A_KillChildren("PhaserRifle");
			}
			"####" # 0 A_Jump(256, "DeathResume")
		Death.Disruptor:
			"####" #  0 {
				A_SetUserVar("user_disintegrationcolor", 2620994);
				A_KillChildren("Disruptor");
			}
			"####" # 0 A_Jump(256, "DeathResume")
		DeathResume:
			"####" # 45 {
				A_Scream;
				A_RemoveChildren(TRUE, 0, "None", "Block");
				ACS_NamedExecuteAlways("A_Disintegrate", 0, user_disintegrationcolor, 1);
				A_ChangeFlag("NOGRAVITY", FALSE);
				A_SetPitch(FRandom(-30.0, 30.0), SPF_INTERPOLATE);
				A_SetRoll(FRandom(-30.0, 30.0), SPF_INTERPOLATE);
			}
			Stop
	}
}

Actor TOSUsableDecoration : TOSSwitchableDecoration
{
	//$Sprite TOS1A0
	-NOTAUTOAIMED 
	+USESPECIAL
}

Actor Skybox1 : TOSDecoration {}
Actor Skybox2 : TOSDecoration {}
Actor Skybox3 : TOSDecoration {}
Actor Skybox4 : TOSDecoration
{
	States
	{
		Static:
			TOS1 A -1 Bright
			Stop
	}
}
Actor Skybox5 : TOSDecoration {}
Actor Skybox6 : TOSDecoration {}
Actor Skybox7 : TOSDecoration { RenderStyle Translucent Alpha 0.7 }